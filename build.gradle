apply plugin: "java"
apply plugin: "maven"
apply plugin: "signing"
apply plugin: "kotlin"
apply plugin: "org.jetbrains.dokka"
apply plugin: 'io.codearte.nexus-staging'

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = "io.github.subiyacryolite"
version = "4.0.5-SNAPSHOT" //major.minor.bugFixIncrement
archivesBaseName = project.name
description = "A dynamic, cross platform, high performance, ORM data-mapper. Designed to assist in rapid development and data mining"
[compileJava, compileTestJava]*.options*.encoding = "UTF-8"

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")
ext.jacksonVersion = '2.9.2'
ext.kotlinVersion = '1.1.60'

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.1.51'
        classpath 'org.jetbrains.dokka:dokka-gradle-plugin:0.9.15'
        classpath 'io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.11.0'
    }
}

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://maven.atlassian.com/3rdparty/" }
    maven { url "http://clojars.org/repo/" }
}

dependencies {
    compile 'org.jetbrains.kotlin:kotlin-stdlib:1.1.60'
    compile 'org.jetbrains.kotlin:kotlin-stdlib-jre8:1.1.60'
    testCompile 'org.jetbrains.kotlin:kotlin-test:1.1.60'
    testCompile 'com.fasterxml.jackson.core:jackson-databind:2.9.1'
    testCompile 'com.fasterxml.jackson.core:jackson-databind:2.9.1'
    testCompile 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.9.1'
    testCompile 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.9.1'
    testCompile 'org.xerial:sqlite-jdbc:3.16.1'
    testCompile 'com.microsoft.sqlserver:mssql-jdbc:6.1.0.jre8'
    testCompile 'org.postgresql:postgresql:42.0.0'
    testCompile 'mysql:mysql-connector-java:6.0.6'
    testCompile 'com.oracle:ojdbc6:12.1.0.1-atlassian-hosted'
    testCompile 'org.junit.jupiter:junit-jupiter-api:5.0.0'
    testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.0.0'
}

jar {
    from configurations.runtime
    manifest {
        attributes(
                "Implementation-Title": "Jenesis Data Store",
                "Implementation-Version": project.version,
                "Created-By": "${System.getProperty("java.version")} (${System.getProperty("java.specification.vendor")})",
        )
    }
}

test {
    //prevent problems with TravisCI
    exclude "**"
}

dokka {
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/javadoc"
}

task javadocJar(type: Jar) {
    classifier = "javadoc"
    from dokka
}

task sourceJar(type: Jar) {
    classifier = "sources"
    from sourceSets.main.allJava
}

artifacts {
    archives javadocJar, sourceJar
}

signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                authentication(userName: hasProperty("ossrhUsername") ? ossrhUsername : "", password: hasProperty("ossrhPassword") ? ossrhPassword : "")
            }
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                authentication(userName: hasProperty("ossrhUsername") ? ossrhUsername : "", password: hasProperty("ossrhPassword") ? ossrhPassword : "")
            }
            pom.project {
                name "Jenesis Data Store"
                packaging "jar"
                description = project.description
                url "https://github.com/SubiyaCryolite/jds"
                scm {
                    connection "scm:git:git@github.com:SubiyaCryolite/jds.git"
                    developerConnection "scm:git:git@github.com:SubiyaCryolite/jds.git"
                    url "https://github.com/SubiyaCryolite/jds"
                }
                licenses {
                    license {
                        name "The 3-Clause BSD License"
                        url "https://opensource.org/licenses/BSD-3-Clause"
                    }
                }
                developers {
                    developer {
                        id "SubiyaCryolite"
                        name "Ifunga Ndana"
                        email "ifungandana@gmail.com"
                    }
                }
            }
        }
    }
}

nexusStaging {
    packageGroup = "io.github.subiyacryolite"
    stagingProfileId = "57cb63f09e7fd5"
}